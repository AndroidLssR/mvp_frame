apply plugin: 'com.android.application'

android {
    def globalConfig = rootProject.extensions.getByName('ext')

    compileSdkVersion globalConfig['androidCompileSdkVersion']
    buildToolsVersion globalConfig['androidBuildToolsVersion']

    defaultConfig {
        minSdkVersion globalConfig['androidMinSdkVersion']
        targetSdkVersion globalConfig['androidTargetSdkVersion']

        applicationId globalConfig['androidApplicationID']
        versionCode globalConfig['androidVersionCode']
        versionName globalConfig['androidVersionName']

        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        globalConfig['globalBuildConfig'].each { config ->
            buildConfigField(config[0], config[1], config[2])
        }
    }

    dexOptions {
        maxProcessCount 4
        javaMaxHeapSize "2g"
    }

    signingConfigs {
        debug {
            storeFile file('../buildsystem/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file('../buildsystem/release.keystore')
            storePassword 'android'
            keyAlias 'release'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug{
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // Include ${module}/libs/*.jar, Do not alone import!
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android official support dependencies
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.supportLibraryVersion"

    // 数据库
    implementation "android.arch.persistence.room:runtime:$rootProject.roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$rootProject.roomVersion"
    implementation "android.arch.persistence.room:rxjava2:$rootProject.roomVersion"

    // Development helpful dependencies
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.leakcanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakcanaryVersion"
    implementation "com.facebook.stetho:stetho:$rootProject.stethoVersion"
    implementation "com.facebook.stetho:stetho-okhttp3:$rootProject.stethoVersion"
    implementation "org.greenrobot:eventbus:$rootProject.eventbugVersion"

    // 网络
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okHttpVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroidVersion"
    implementation "com.trello.rxlifecycle2:rxlifecycle:$rootProject.rxLifecycleVersion"
    implementation "com.trello.rxlifecycle2:rxlifecycle-android-lifecycle:$rootProject.rxLifecycleVersion"

    // 依赖注入
    implementation "com.google.dagger:dagger:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    implementation "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"

//    implementation "com.google.guava:guava:$rootProject.guavaVersion"

    // Module dependencies
    implementation "com.facebook.fresco:fresco:$rootProject.frescoVersion"
    implementation "com.facebook.fresco:imagepipeline-okhttp3:$rootProject.frescoVersion"
    implementation "com.facebook.fresco:animated-gif:$rootProject.frescoVersion"
    implementation "com.journeyapps:zxing-android-embedded:$rootProject.zXingVersion"
    implementation "com.zhihu.android:matisse:$rootProject.matisseVersion"
    implementation "com.tbruyelle.rxpermissions2:rxpermissions:$rootProject.rxpermissionsVersion"
    implementation "com.liulishuo.filedownloader:library:$rootProject.filedownloaderVersion"
    implementation "com.github.nanchen2251:CompressHelper:$rootProject.compressHelperVersion"
    // UI dependencies
    implementation "com.classic.common:multiple-status-view:$rootProject.multipleStatusVersion"
    implementation "com.scwang.smartrefresh:SmartRefreshLayout:$rootProject.refreshLayoutVersion"
    implementation "com.scwang.smartrefresh:SmartRefreshHeader:$rootProject.refreshLayoutVersion"

    // Test dependencies
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })
    testImplementation "junit:junit:$rootProject.junitVersion"
}
